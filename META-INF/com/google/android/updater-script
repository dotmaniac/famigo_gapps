# PREP
package_extract_file("prepare-install.sh", "/tmp/prepare-install.sh");
set_perm(0, 0, 0777, "/tmp/prepare-install.sh");
run_program("/tmp/prepare-install.sh", "");

# EXTRA WORK
set_progress(0.17);
# Removing pieces that may be left over from other GApps or ROM's
	delete(
		"/system/app/BrowserProviderProxy.apk",
		"/system/app/Calendar.apk",
		"/system/app/Gmail.apk",
		"/system/app/GmsCore.apk",
		"/system/app/GoogleCalendar.apk",
		"/system/app/GoogleCalendarSyncAdapter.apk",
		"/system/app/GoogleCloudPrint.apk",
		"/system/app/GoogleHangouts.apk",
		"/system/app/GoogleKeep.apk",
		"/system/app/GooglePlus.apk",
		"/system/app/PartnerBookmarksProvider.apk",
		"/system/app/QuickSearchBox.apk",
		"/system/app/Talk.apk",
		"/system/app/Vending.apk",
		"/system/app/Youtube.apk",
		"/system/priv-app/Calendar.apk",
		"/system/priv-app/GmsCore.apk",
		"/system/priv-app/GoogleNow.apk",
		"/system/priv-app/OneTimeInitializer.apk",
		"/system/priv-app/QuickSearchBox.apk",
		"/system/priv-app/Vending.apk"
	);

# Remove .odex files since they're no longer needed
	delete(
		"/system/app/CalendarGoogle.odex",
		"/system/app/GenieWidget.odex",
		"/system/app/Gmail2.odex",
		"/system/app/GoogleContactsSyncAdapter.odex",
		"/system/app/GoogleEars.odex",
		"/system/app/GoogleTTS.odex",
		"/system/app/Keep.odex",
		"/system/app/MediaUploader.odex",
		"/system/framework/com.google.android.maps.odex",
		"/system/framework/com.google.android.media.effects.odex",
		"/system/framework/com.google.widevine.software.drm.odex",
		"/system/priv-app/CalendarProvider.odex",
		"/system/priv-app/GoogleBackupTransport.odex",
		"/system/priv-app/GoogleFeedback.odex",
		"/system/priv-app/GoogleLoginService.odex",
		"/system/priv-app/GooglePartnerSetup.odex",
		"/system/priv-app/GoogleServicesFramework.odex",
		"/system/priv-app/OneTimeInitializer.odex",
		"/system/priv-app/SetupWizard.odex"
	);

# Removing libs that are no longer included with Google+ (check to make certain it doesn't show up in another app)
	delete(
		"/system/lib/libstlport_shared.so",
		"/system/lib/libfacetracker.so",
		"/system/lib/libfrsdk.so"
	);

if is_substring("4.4", file_getprop("/system/build.prop","ro.build.version.release")) then
	# Remove apps from 'app' that need to be installed in 'priv-app'
		delete(
			"/system/app/CalendarProvider.apk",
			"/system/app/GoogleBackupTransport.apk",
			"/system/app/GoogleFeedback.apk",
			"/system/app/GoogleLoginService.apk",
			"/system/app/GoogleOneTimeInitializer.apk",
			"/system/app/GooglePartnerSetup.apk",
			"/system/app/GoogleServicesFramework.apk",
			"/system/app/OneTimeInitializer.apk",
			"/system/app/Phonesky.apk",
			"/system/app/PrebuiltGmsCore.apk",
			"/system/app/SetupWizard.apk",
			"/system/app/talkback.apk",
			"/system/app/Velvet.apk",
			"/system/app/Wallet.apk"
		);

	# Removing MediaUploader (since Google removed it beginning with 4.4.1)
		delete(
			"/system/app/MediaUploader.apk",
			"/system/priv-app/MediaUploader.apk"
		);
endif;

# CORE APPS
set_progress(0.23);
ui_print("Installing Core Apps");
package_extract_dir("Core/Shared", "/system");
if is_substring("4.3", file_getprop("/system/build.prop","ro.build.version.release")) then
	package_extract_dir("Core/4.3.x", "/system");
	package_extract_dir("Core/priv-app", "/system/app");
endif;
if is_substring("4.4", file_getprop("/system/build.prop","ro.build.version.release")) then
	package_extract_dir("Core/4.4.x", "/system");
	package_extract_dir("Core/priv-app", "/system/priv-app");
endif;

# OPTIONALS
    # Extract photosphere installation files if supported or requested
    ifelse(
        file_getprop("/tmp/info.prop","install.photosphere") == "true",
        (
            # extract photosphere installation files
            package_extract_dir("optional/photosphere", "/tmp/photosphere");
        )
    );
    
    # Extract faceunlock installation files if supported
    ifelse(
        file_getprop("/tmp/info.prop","install.faceunlock") == "true",
        (
            if is_substring("4.3", file_getprop("/system/build.prop","ro.build.version.release")) then
    	        package_extract_dir("optional/face/4.3.x", "/tmp/face");
            endif;
            if is_substring("4.4", file_getprop("/system/build.prop","ro.build.version.release")) then
    	        package_extract_dir("optional/face/4.4.x", "/tmp/face");
            endif;
        )
    );
    
    # If faceunlock or photosphere installation is needed then run install-optional script
    ifelse(
        file_getprop("/tmp/info.prop","install.faceunlock") == "true"||file_getprop("/tmp/info.prop","install.photosphere") == "true",
        (
            package_extract_file("install-optional.sh", "/tmp/install-optional.sh");
            set_perm(0, 0, 0777, "/tmp/install-optional.sh");
            run_program("/tmp/install-optional.sh", file_getprop("/tmp/info.prop","install.faceunlock"));
        )
    );

# GAPPS
ui_print("Installing Google Apps");
# Google+
set_progress(0.27);
ui_print("	Plus");
if file_getprop("/tmp/aroma/gapps.prop", "item.1.1") == "1" then
	ui_print("		Google+");
	package_extract_dir("gapps/Plus/Plus", "/system");
endif;

if file_getprop("/tmp/aroma/gapps.prop", "item.1.2") == "1" then
	ui_print("		Hangouts");
	package_extract_dir("gapps/Plus/Hangouts", "/system");
endif;

# Google Drive
set_progress(0.29);
ui_print("	Drive");
if file_getprop("/tmp/aroma/gapps.prop", "item.2.1") == "1" then
	ui_print("		Drive");
	package_extract_dir("gapps/Drive/Drive", "/system");
endif;

if file_getprop("/tmp/aroma/gapps.prop", "item.2.2") == "1" then
	ui_print("		Keep");
	package_extract_dir("gapps/Drive/Keep", "/system");
endif;

# Google Play
set_progress(0.35);
ui_print("	Play");
if file_getprop("/tmp/aroma/gapps.prop", "item.3.1") == "1" then
	ui_print("		Books");
	package_extract_dir("gapps/Play/Books", "/system");
endif;

if file_getprop("/tmp/aroma/gapps.prop", "item.3.2") == "1" then
	ui_print("		Games");
	package_extract_dir("gapps/Play/Games", "/system");
endif;

if file_getprop("/tmp/aroma/gapps.prop", "item.3.3") == "1" then
	ui_print("		Movies & TV");
	package_extract_dir("gapps/Play/Videos", "/system");
endif;

if file_getprop("/tmp/aroma/gapps.prop", "item.3.4") == "1" then
	ui_print("		Music");
	package_extract_dir("gapps/Play/Music", "/system");
endif;

if file_getprop("/tmp/aroma/gapps.prop", "item.3.5") == "1" then
	ui_print("		Newsstand");
	package_extract_dir("gapps/Play/News", "/system");
	# Deleting Google Currents
	delete("/system/app/Currents.apk","/system/priv-app/Currents.apk");
endif;

# Google Search
set_progress(0.43);
ui_print("	Search");
if file_getprop("/tmp/aroma/gapps.prop", "item.4.1") == "1" then
	ui_print("		Chrome");
	package_extract_dir("gapps/Search/Chrome", "/system");
endif;

if file_getprop("/tmp/aroma/gapps.prop", "item.4.2") == "1" then
	ui_print("		Home");
	package_extract_dir("gapps/Search/GEL", "/system");
	# Installing Google Now
	package_extract_dir("gapps/Search/Now/lib", "/system/lib");
	package_extract_dir("gapps/Search/Now/addon.d", "/system/addon.d");
	if is_substring("4.3", file_getprop("/system/build.prop","ro.build.version.release")) then
		package_extract_dir("gapps/Search/Now/app", "/system/app");
	endif;
	if is_substring("4.4", file_getprop("/system/build.prop","ro.build.version.release")) then
		package_extract_dir("gapps/Search/Now/app", "/system/priv-app");
	endif;
endif;

if file_getprop("/tmp/aroma/gapps.prop", "item.4.3") == "1" then
	ui_print("		Maps");
	package_extract_dir("gapps/Search/Maps", "/system");
endif;

if file_getprop("/tmp/aroma/gapps.prop", "item.4.4") == "1" then
	ui_print("		Now");
	package_extract_dir("gapps/Search/Now/lib", "/system/lib");
	package_extract_dir("gapps/Search/Now/addon.d", "/system/addon.d");
	if is_substring("4.3", file_getprop("/system/build.prop","ro.build.version.release")) then
		package_extract_dir("gapps/Search/Now/app", "/system/app");
	endif;
	if is_substring("4.4", file_getprop("/system/build.prop","ro.build.version.release")) then
		package_extract_dir("gapps/Search/Now/app", "/system/priv-app");
	endif;
endif;

# Other GApps
set_progress(0.51);
ui_print("	Other");
if file_getprop("/tmp/aroma/gapps.prop", "item.5.1") == "1" then
	ui_print("		Calendar");
	package_extract_dir("gapps/Other/Calendar", "/system");
endif;

if file_getprop("/tmp/aroma/gapps.prop", "item.5.2") == "1" then
	ui_print("		Gmail");
	package_extract_dir("gapps/Other/Gmail", "/system");
endif;

if file_getprop("/tmp/aroma/gapps.prop", "item.5.3") == "1" then
	ui_print("		Keyboard");
	package_extract_dir("gapps/Other/Keyboard", "/system");
endif;

if file_getprop("/tmp/aroma/gapps.prop", "item.5.4") == "1" then
	ui_print("		Wallet");
	package_extract_dir("gapps/Other/Wallet/addon.d", "/system/addon.d");
	if is_substring("4.3", file_getprop("/system/build.prop","ro.build.version.release")) then
		package_extract_dir("gapps/Other/Wallet/app", "/system/app");
	endif;
	if is_substring("4.4", file_getprop("/system/build.prop","ro.build.version.release")) then
		package_extract_dir("gapps/Other/Wallet/app", "/system/priv-app");
	endif;
endif;

if file_getprop("/tmp/aroma/gapps.prop", "item.5.5") == "1" then
	ui_print("		YouTube");
	package_extract_dir("gapps/Other/YouTube", "/system");
endif;

# REMOVER
    # Stock
    if file_getprop("/tmp/aroma/remover.prop","selected") == "1" then
            package_extract_file("remover/stock.sh", "/tmp/stock.sh");
            package_extract_file("remover/junk-stock.txt", "/sdcard/Famigo/junk-stock.txt");
            set_perm(0, 0, 0777, "/tmp/stock.sh");
            run_program("/tmp/stock.sh");
    		ui_print(" Running Stock Remover");
    endif;
    
    # Custom
    if file_getprop("/tmp/aroma/remover.prop","selected") == "2" then
            ifelse((less_than_int(run_program("/sbin/busybox", "test", "-e", "/sdcard/Famigo/junk.txt"), 1);),(ui_print("  -Removing selected apps...");),(ui_print("  -FAILED!!! Check Famigo folder for junk.txt");));
            package_extract_file("remover/custom.sh", "/tmp/custom.sh");
            set_perm(0, 0, 0777, "/tmp/custom.sh");
            run_program("/tmp/custom.sh");
    endif;

# PERMS
ui_print("Setting Permissions");
set_perm_recursive(0, 0, 0755, 0644, "/system/addon.d");
set_perm_recursive(0, 0, 0755, 0644, "/system/app");
if is_substring("4.4", file_getprop("/system/build.prop","ro.build.version.release")) then
	set_perm_recursive(0, 0, 0755, 0644, "/system/priv-app");
endif;

# UNMOUNT
ui_print("Unmounting system...");
run_program("/sbin/busybox", "umount", "/system");

# FINISH
ui_print("Finished Install");
set_progress(1);
